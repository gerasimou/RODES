// Workstation cluster [HHK00]
// dxp/gxn 11/01/00

ctmc

const int N=20; // Number of workstations in each cluster
const int left_mx = N; // Number of work stations in left cluster
const int right_mx = N; // Number of work stations in right cluster
const int init_N = 10;

// Minimum QoS requires 3/4 connected workstations operational
const int k = floor(0.75*N);
const int l = floor(1.25*N);
formula minimum = (left_n>=k & toleft_n) | 
                  (right_n>=k & toright_n) | 
                  ((left_n+right_n)>=k & toleft_n & line_n & toright_n);

label "minimum" = (left_n>=k & toleft_n) | (right_n>=k & toright_n) | ((left_n+right_n)>=k & toleft_n & line_n & toright_n);
label "premium" = (left_n+right_n)>=l & toleft_n & line_n & toright_n;


// Failure rates
const double ws_fail; // Single workstation: average time to fail = 500 hrs [0.001,051]
const double switch_fail = 1/4000; // Switch: average time to fail = 4000 hrs
const double line_fail = 1/5000; // Backbone: average time to fail = 5000 hrs

// Inspection rates (more interesting, these are controllable while failures in general are not)
// One might find e.g. that is sufficient to check the backbone less often than the workstations
const double ws_check; //[0.1,5.1]
const double switch_check = 10;
const double line_check = 10;

const double ws_repair; //[0.01,1.01]

// One could even have a reward that counts the number of working hours of the repairman and try to minimise them
// like R{"working_hours"}=?[F minimum] starting from a state where !minimum holds

// Left cluster
module Left 
	
	left_n : [0..left_mx] init init_N; // Number of workstations operational
	left : bool; // Being repaired?
	
	[startLeft] !left & (left_n<left_mx) -> 1 : (left'=true);
	[repairLeft] left & (left_n<left_mx) -> 1 : (left'=false) & (left_n'=left_n+1);
	[] (left_n>0) -> ws_fail*left_n : (left_n'=left_n-1);
	
endmodule

// Right cluster
module Right = Left[left_n=right_n,
                    left=right,
                    left_mx=right_mx,
                    startLeft=startRight,
                    repairLeft=repairRight ]
endmodule

// Repair unit
module Repairman
	
	r : bool; // Repairing?
	
	[startLeft]    !r -> ws_check : (r'=true); // Inspect Left 
	[startRight]   !r -> ws_check : (r'=true); // Inspect Right 
	[startToLeft]  !r -> switch_check : (r'=true); // Inspect ToLeft
	[startToRight] !r -> switch_check : (r'=true); // Inspect ToRight 
	[startLine]    !r -> ws_check : (r'=true); // Inspect Line 
	
	[repairLeft]    r -> ws_repair     : (r'=false); // Repair Left 
	[repairRight]   r -> ws_repair     : (r'=false); // Repair Right
	[repairToLeft]  r -> 0.25  : (r'=false); // Repair ToLeft
	[repairToRight] r -> 0.25  : (r'=false); // Repair ToRight
	[repairLine]    r -> 0.125 : (r'=false); // Repair Line
	
endmodule

// Line/backbone
module Line 
	
	line :   bool; // Being repaired?
	line_n : bool init true; // Working?
	
	[startLine] !line & !line_n -> 1 : (line'=true);
	[repairLine] line & !line_n -> 1 : (line'=false) & (line_n'=true);
	[] line_n -> line_fail : (line_n'=false);
	
endmodule

// Left switch
module ToLeft = Line[line=toleft,
                     line_n=toleft_n,
                     line_fail=switch_fail,
                     startLine=startToLeft,
                     repairLine=repairToLeft ]
endmodule

// Right switch
module ToRight = Line[line=toright,
                      line_n=toright_n,
                      line_fail=switch_fail,
                      startLine=startToRight,
                      repairLine=repairToRight ]
endmodule

// Reward structures

// Percentage of operational workstations stations
rewards "percent_op"
	true : 100*(left_n+right_n)/(2*N);
endrewards

// Time that the system is not delivering at least minimum QoS
rewards "time_not_min"
	!minimum : 1; 
endrewards

// Number of repairs
rewards "num_repairs"
	[repairLeft]    true : 1;
	[repairRight]   true : 1;
	[repairToLeft]  true : 1;
	[repairToRight] true : 1;
	[repairLine]    true : 1;
endrewards

// Number of repairs
rewards "working_hours"
	r:1;
endrewards

// Number of repairs
rewards "cost"
	//r:left_n + right_n;
	//!r: left_n + right_n;
	[repairToLeft]  true : left_n ;
	[repairToRight] true : right_n;
	[repairLine]    true : left_n + right_n;
endrewards


rewards "cost1"
	r:left_n + right_n;
	//!r: left_n + right_n;
endrewards

rewards "cost2"
	[repairLeft]    true : 1;
	[repairRight]   true : 1;
	[repairToLeft]  true : 20*left_n ;
	[repairToRight] true : 20*right_n;
	[repairLine]    true : 50*(left_n + right_n);
endrewards


